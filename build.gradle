buildscript {
    repositories {
        google()
        mavenCentral()
        flatDir { dirs 'app/libs' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'org.owasp:dependency-check-gradle:6.1.6'

        // Huawei agconnect plugin
        classpath 'com.huawei.agconnect:agcp-1.4.2.300'
        classpath 'com.huawei.agconnect:agconnect-crash-symbol-lib-1.4.2.300'
        classpath 'com.huawei.agconnect:agconnect-core-1.4.0.300@aar'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        flatDir { dirs 'libs' }

        // Huawei
        exclusiveContent {
            forRepository {
                maven { url 'https://developer.huawei.com/repo/' }
            }
            filter {
                // Only matching dependencies will be installed from this repository.
                includeGroup "com.huawei.hms"
                includeGroup "com.huawei.android.hms"
                includeGroup "com.huawei.hmf"
            }
        }
    }

    // OWASP dependency-check-gradle plugin
    apply plugin: 'org.owasp.dependencycheck'
    dependencyCheck {
        skipConfigurations += 'lintClassPath'
        suppressionFile 'dependencyCheckSuppressions.xml'
        // Fail dependency check if any dependency has a CVE with a score of 3+
        failBuildOnCVSS 3
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
